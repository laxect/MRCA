# Maintainer: Gyara <laxect39@gmail.com>

pkgname=changeup-git
_pkgname=changeup
pkgver=r12.6bd0b55
pkgrel=1
pkgdesc="My sway tools"
url="https://git.sr.ht/~fubuki/changeup/"
makedepends=('git' 'cargo')
arch=('i686' 'x86_64')
license=('MIT')
source=("git+$url")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname/"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname/"
  cargo +nightly build --bins --release --out-dir=target -Z unstable-options
}

package() {
  cd "$srcdir/$_pkgname/"
  install -Dm755 "target/changeup" "$pkgdir/usr/bin/changeup"
  install -Dm755 "target/client" "$pkgdir/usr/bin/changeup-client"
  install -Dm0644 "changeup.service" "$pkgdir/usr/lib/systemd/user/changeup.service"
  install -Dm0644 "moe.gyara.changeup.service" "$pkgdir/usr/share/dbus-1/services/moe.gyara.changeup.service"
}
